#version 330 core

uniform mat4 modelMatrix;
uniform vec3 cameraPosition;
//texture
uniform sampler2D tex;
uniform vec2 texRatio = vec2(1,1);
//specular stuff
uniform float ambientIntensity;
uniform float shininess;
uniform vec3 specularColor;
//light 
#define MAX_LIGHTS 10
uniform int numLights = 1;
uniform struct Light
{
	//standard light stuff
	vec4 pos;
	vec3 color;
	float attenuation;
	float coneAngle;
	vec3 coneDirection;
} allLights[MAX_LIGHTS];

float lodRange[4] = float[](9,60,100,250);

in vec3 vertPositions;
in vec2 vertUVs;
in vec3 vertNormals;

out vec4 finalColor;

vec3 ApplyLight(Light light, vec3 normal, vec3 surfacePos, vec3 surfaceCol, vec3 surfaceToCam)
{
	//get surface to light vector first
	vec3 surfaceToLight;// = normalize(light.pos - surfacePos);
	float attenuation = 1.0;
	
	if (light.pos.w == 0)
	{
		//directional light!
		surfaceToLight = normalize(light.pos.xyz);//infinite direction
		attenuation = 1.0;
	}
	else
	{
		//either point or cone
		surfaceToLight = normalize(light.pos.xyz - surfacePos);
		//attenuation exists for these two
		float distanceToLight = length(light.pos.xyz - surfacePos);
		attenuation = 1.0 / (1.0 + light.attenuation * pow(distanceToLight, 2));
		
		//cones if possible
		if (light.coneAngle > 0.0)
		{
			float lightToSurfaceAngle = degrees(acos(dot(-surfaceToLight, normalize(light.coneDirection))));
			if (lightToSurfaceAngle > light.coneAngle)
			{
				attenuation = 0.0;
			}
		}
	}
	
	//cosine of angle of incidence
	float diffuseCoef = max(0.0, dot(normal, surfaceToLight));
	vec3 diffuse = diffuseCoef * light.color * surfaceCol.rgb;
	vec3 ambient = ambientIntensity * surfaceCol.rgb * light.color;
		
	//now the specular calculations
	vec3 incidence = -(surfaceToLight);
	vec3 reflection = reflect(incidence, normal);	
	
	float cosAngle = max(0.0, dot(surfaceToCam, reflection));
	//only have specular color if there is diffuse color as well 
	float specularCoef = 0.0;
	if (diffuseCoef > 0.0)
		specularCoef = pow(cosAngle, shininess);
		
	vec3 specular = specularCoef * specularColor * light.color;
	return ambient + attenuation * (diffuse + specular);
}

void main()
{
	//get normal in world coords
	vec3 normal = normalize(transpose(inverse(mat3(modelMatrix))) * vertNormals);
	vec3 surfacePos = vec3(modelMatrix * vec4(vertPositions, 1));
	vec2 adjustedVertUVs = vertUVs * texRatio;
	vec3 surfaceToCamera = normalize(cameraPosition - surfacePos);
		
	//Basic LOD
	float distanceToCamera = length(surfaceToCamera);
	
	vec2 tileScale = vec2 (1.0);
	if (distanceToCamera < lodRange[0])
	{
		tileScale = texRatio * 5.0;
	}
	else if (distanceToCamera < lodRange[1])
	{
		tileScale = texRatio * 2.5;
	}
	else if (distanceToCamera < lodRange[2])
	{
		tileScale = texRatio * 1.5;
	}
	else if (distanceToCamera < lodRange[3])
	{
		tileScale = texRatio * 0.5;
	}
	else
	{
		tileScale = texRatio * 0.2;
	}
	
	vec2 adjustedUVs = vertUVs * tileScale;
	vec4 surfaceColor = texture(tex, adjustedUVs);
		
	vec3 linear = vec3 (0);
	for ( int i = 0; i < numLights; ++i)
	{
		linear += ApplyLight( allLights[i], normal, surfacePos, surfaceColor.rgb, surfaceToCamera);
	}
	//linear = ApplyLight(light, normal, surfacePos, surfaceColor.rgb, surfaceToCamera);
		
	vec3 gamma = vec3 (1.0 / 2.2);	
	finalColor = vec4 (pow(linear, gamma), surfaceColor.a);
}











